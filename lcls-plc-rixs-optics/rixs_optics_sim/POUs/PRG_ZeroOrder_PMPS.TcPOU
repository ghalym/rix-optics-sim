<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="PRG_ZeroOrder_PMPS" Id="{0e0e31f6-e121-41d2-ab29-6a7971176303}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM PRG_ZeroOrder_PMPS
VAR
	// PMPS
	ffZeroOrderBeam : FB_FastFault := (
        i_xAutoReset := TRUE,
        i_DevName := 'Zero Order Beam',
        i_Desc := 'Faults when Zero order beam safe conditions are not met',
        i_TypeCode := 16#F506);	
	{attribute 'pytmc' := '
        pv: SIM:SafeBenderRange
        field: ZNAM FALSE
        field: ONAM TRUE
    '}
    bSafeBenderRange : BOOL;
	
	{attribute 'pytmc' := '
        pv: SIM:LRG_Grating_IN
        field: ZNAM FALSE
        field: ONAM TRUE
    '}
    bLRG_Grating_IN : BOOL;
	
	{attribute 'pytmc' := '
        pv: SIM:ZOS_IN
        field: ZNAM FALSE
        field: ONAM TRUE
    '}
    bZOS_IN : BOOL;
	
	{attribute 'pytmc' := '
        pv: SIM:ZOB_on_Lower_Stopper
        field: ZNAM FALSE
        field: ONAM TRUE
    '}
    bZOB_on_Lower_Stopper : BOOL;
	
	{attribute 'pytmc' := '
        pv: SIM:MR1K1_Inserted
        field: ZNAM FALSE
        field: ONAM TRUE
    '}
    bMR1K1_Inserted : BOOL;
	
	{attribute 'pytmc' := '
        pv: SIM:ZeroOrderBeam
        field: ZNAM FALSE
        field: ONAM TRUE
    '}
    bZeroOrderBeam : BOOL;

END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[
ffZeroOrderBeam.i_xOK := SafeBenderRange() OR (LRG_Grating_IN() AND ZOS_IN() AND ZOB_on_Lower_Stopper() AND MR1K1_Inserted());
ffZeroOrderBeam(io_fbFFHWO := GVL_PMPS.fbFastFaultOutput1);


//Diagnostics
bSafeBenderRange := SafeBenderRange();
bLRG_Grating_IN := LRG_Grating_IN();
bZOS_IN := ZOS_IN();
bZOB_on_Lower_Stopper := ZOB_on_Lower_Stopper();
bMR1K1_Inserted := MR1K1_Inserted();
bZeroOrderBeam := ffZeroOrderBeam.i_xOK;]]></ST>
    </Implementation>
    <Action Name="ACT_CALC" Id="{d5fce001-4a3d-419c-af0a-bef186506e22}">
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Action>
    <Method Name="LRG_Grating_IN" Id="{3f08ef3a-1a67-4db8-aaa1-f5005be117c4}">
      <Declaration><![CDATA[METHOD LRG_Grating_IN : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[LRG_Grating_IN := (M9.fPosition >= 24000) AND (M9.fPosition <= 28000);]]></ST>
      </Implementation>
    </Method>
    <Method Name="MR1K1_Inserted" Id="{f7ec4dc7-28fa-49b0-bff7-fb2b3a3102db}">
      <Declaration><![CDATA[METHOD MR1K1_Inserted : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[MR1K1_Inserted := PMPS_GVL.stCurrentBeamParameters.aStoppers[PMPS.K_Stopper.MR1K1_IN] 
                    AND NOT PMPS_GVL.stCurrentBeamParameters.aStoppers[PMPS.K_Stopper.MR1K1_OUT];]]></ST>
      </Implementation>
    </Method>
    <Method Name="SafeBenderRange" Id="{e35f2611-b917-4b4b-b923-ab3f3098b1a0}">
      <Declaration><![CDATA[METHOD SafeBenderRange : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ SafeBenderRange :=  GVL_M1K1_BENDER_Constants.nM1K1bendUS_PMPS_LowerLimit < M17.nEncoderCount AND M17.nEncoderCount < GVL_M1K1_BENDER_Constants.nM1K1bendUS_PMPS_UpperLimit AND
    GVL_M1K1_BENDER_Constants.nM1K1bendDS_PMPS_LowerLimit < M18.nEncoderCount AND M18.nEncoderCount < GVL_M1K1_BENDER_Constants.nM1K1bendDS_PMPS_UpperLimit;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ZOB_on_Lower_Stopper" Id="{834e9b9f-3b7f-4552-a942-f4f34b4f9a1d}">
      <Declaration><![CDATA[METHOD ZOB_on_Lower_Stopper : BOOL
VAR_INPUT
END_VAR
VAR
	Pm3 : LREAL;
	Pm2: LREAL;
	Pm1: LREAL;
	Hm1: LREAL;
	HZos:LREAL;
	Zbm1: LREAL;
	Zbm2: LREAL;
	Zbm3: LREAL;
	Hbm1: LREAL;
	Hbm2: LREAL;
	Hbm3: LREAL;
	Hb0m3:LREAL;
	//fixed calc
	Hm3	 : LREAL;
	Hpiv : LREAL;
	Theta_m1: LREAL;
	Theta_m2: LREAL;
	Theta_m3: LREAL;
	Delta : LREAL;
	Ans : LREAL;
END_VAR

VAR CONSTANT
	Hi2 :LREAL:=1.4;//m
	Zi2: LREAL :=731.613;//m
	Theta0 : LREAL:=0;
	Zm1 : LREAL:=733.772;//m
	Zmon : LREAL:=739.622;//m
	Zpiv : LREAL:= 739.622 ; //m
	Zzos : LREAL:= 741.422;//m
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[

//Hm1 = 1400000+mr1k1 ben Y up
Hm1:= 1400000+M12.fPosition;
//Pm1 = 18310 - 281 + mr1k1 bend pitch 
Pm1 := 18310 - 281 + Main.M16.fPosition;
//Hpiv = 1.620305 + 0.001 // m 0.001 is correction from nominal
Hpiv:= 1.620305 + 0.001;
//Pm2 = mono mirror pitch - 90700 urad
Pm2 := Main.M6.fPosition - 90700;
//Hm3 = Hpiv + 0.006 m
Hm3:= Hpiv +0.006;
//Pm3 = mono grating pitch - 63300 urad
Pm3 := Main.M7.fPosition - 63300;
//Hzos = 1824.3 + st1k1_zos mm
Hzos:= 1824.3 + M24.fPosition;



Theta_m1 := 2*Pm1 - Theta0;
Theta_m2 := 2*Pm2 - Theta_m1;
Theta_m3 := 2 *Pm3 - 2 * Pm2 + 2* Pm1 - Theta0;


Zbm1 := (Hm1 - TAN(Pm1)* Zm1 - Hi2 + TAN(Theta0) *Zi2)/(TAN(Theta0) - TAN(Pm1));
hbm1 := TAN(Pm1)*Zbm1 + Hm1 - TAN(Pm1)*Zm1;

Zbm2 := (Hbm1 - TAN(Theta_m1)*Zbm1 - Hpiv + TAN(Pm2)*Zpiv)/ (TAN(Pm2) - TAN(Theta_m1));
Hbm2 := TAN(Pm2)*Zbm2 + Hpiv - TAN(Pm2)*Zpiv;

Zbm3 := (Hbm2 - TAN(Theta_m2)*Zbm2 - Hm3 + TAN(Pm3)*Zpiv)/ (TAN(Pm3) - TAN(Theta_m2));
Hbm3 := TAN(Pm3) * Zbm3 + Hm3 - TAN(Pm3)*Zpiv;

Hb0m3 := TAN(Theta_m3)*Zzos + Hbm3 - TAN(Theta_m3)*Zbm3;





//TRUE if hb0m3(Zzoz) - Hzos > D
Ans := Hb0m3 - Hzos;
IF GVL_PMPS.rPhotonEnergy <550 THEN
	ZOB_on_Lower_Stopper := Ans > 6;
	ELSIF GVL_PMPS.rPhotonEnergy <1000 THEN
		ZOB_on_Lower_Stopper := Ans >  3.2;
	ELSIF GVL_PMPS.rPhotonEnergy >=1000 THEN
		ZOB_on_Lower_Stopper := Ans >  2.5;	
		
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ZOS_IN" Id="{594f8399-9c5f-4892-896e-d33ecff953b6}">
      <Declaration><![CDATA[METHOD ZOS_IN : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF GVL_PMPS.rPhotonEnergy >1500 THEN ZOS_IN:= FALSE;
	ELSIF (GVL_PMPS.rPhotonEnergy >1000 AND M24.fPosition>= 8.5) THEN ZOS_IN:= TRUE;
		ELSIF (GVL_PMPS.rPhotonEnergy <=1000 AND GVL_PMPS.rPhotonEnergy >550 AND M24.fPosition>= 7.9) THEN ZOS_IN:= TRUE;
			ELSIF (GVL_PMPS.rPhotonEnergy <=550 AND GVL_PMPS.rPhotonEnergy >200 AND M24.fPosition>= 6.9) THEN ZOS_IN:= TRUE;
				ELSE ZOS_IN:= FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>